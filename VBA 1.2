Sub CalcularProporcaoIndividualPorBacia()

    Dim wsFonte As Worksheet
    Dim wsDestino As Worksheet
    Dim ultimaLinha As Long
    Dim i As Long
    Dim anoAtual As Variant
    Dim bacia As String
    Dim producao As Double

    Dim dictTotalGeral As Object
    Dim dictCampos As Object, dictSantos As Object, dictES As Object

    Dim linhaDestino As Long

    Set wsFonte = ThisWorkbook.Sheets("Produção Por Bacia")
    Set wsDestino = ThisWorkbook.Sheets("Graficos")

    Set dictTotalGeral = CreateObject("Scripting.Dictionary")
    Set dictCampos = CreateObject("Scripting.Dictionary")
    Set dictSantos = CreateObject("Scripting.Dictionary")
    Set dictES = CreateObject("Scripting.Dictionary")

    ultimaLinha = wsFonte.Cells(wsFonte.Rows.Count, 1).End(xlUp).Row

    ' Limpa dados antigos
    wsDestino.Range("A2:H1000").ClearContents
    wsDestino.Range("I:I").Clear

    ' Preencher dicionários com dados
    For i = 2 To ultimaLinha
        If IsNumeric(wsFonte.Cells(i, 1).Value) Then
            anoAtual = wsFonte.Cells(i, 1).Value
            bacia = Trim(wsFonte.Cells(i, 2).Value)
            producao = Val(wsFonte.Cells(i, 5).Value)

            If bacia = "Total Geral" Then
                dictTotalGeral(anoAtual) = producao
            Else
                Select Case bacia
                    Case "Campos"
                        If Not dictCampos.exists(anoAtual) Then dictCampos(anoAtual) = 0
                        dictCampos(anoAtual) = dictCampos(anoAtual) + producao
                    Case "Santos"
                        If Not dictSantos.exists(anoAtual) Then dictSantos(anoAtual) = 0
                        dictSantos(anoAtual) = dictSantos(anoAtual) + producao
                    Case "Espírito Santo"
                        If Not dictES.exists(anoAtual) Then dictES(anoAtual) = 0
                        dictES(anoAtual) = dictES(anoAtual) + producao
                End Select
            End If
        End If
    Next i

    ' Cabeçalho
    wsDestino.Range("A1:H1").Value = Array("Ano", "Campos", "Santos", "Espírito Santo", _
                                           "Total Geral", "Prop. Campos", "Prop. Santos", "Prop. Espírito Santo")

    linhaDestino = 2
    For Each anoAtual In dictTotalGeral.Keys
        Dim camposVal As Double, santosVal As Double, esVal As Double
        camposVal = 0: santosVal = 0: esVal = 0

        If dictCampos.exists(anoAtual) Then camposVal = dictCampos(anoAtual)
        If dictSantos.exists(anoAtual) Then santosVal = dictSantos(anoAtual)
        If dictES.exists(anoAtual) Then esVal = dictES(anoAtual)

        Dim totalVal As Double
        totalVal = dictTotalGeral(anoAtual)

        ' Preencher valores
        wsDestino.Cells(linhaDestino, 1).Value = anoAtual
        wsDestino.Cells(linhaDestino, 2).Value = camposVal
        wsDestino.Cells(linhaDestino, 3).Value = santosVal
        wsDestino.Cells(linhaDestino, 4).Value = esVal
        wsDestino.Cells(linhaDestino, 5).Value = totalVal

        If totalVal <> 0 Then
            wsDestino.Cells(linhaDestino, 6).Value = camposVal / totalVal
            wsDestino.Cells(linhaDestino, 7).Value = santosVal / totalVal
            wsDestino.Cells(linhaDestino, 8).Value = esVal / totalVal

            wsDestino.Range(wsDestino.Cells(linhaDestino, 6), wsDestino.Cells(linhaDestino, 8)).NumberFormat = "0.00%"
        End If

        ' Aplica formatação alternada (linhas 2 e 3 como referência)
        If linhaDestino > 3 Then
            Dim fonteLinha As Long
            fonteLinha = 2 + ((linhaDestino - 2) Mod 2) ' alterna entre 2 e 3
            wsDestino.Range("A" & fonteLinha & ":H" & fonteLinha).Copy
            wsDestino.Range("A" & linhaDestino & ":H" & linhaDestino).PasteSpecial Paste:=xlPasteFormats
            Application.CutCopyMode = False
        End If

        linhaDestino = linhaDestino + 1
    Next

    MsgBox "Proporções individuais calculadas com sucesso!"

End Sub












